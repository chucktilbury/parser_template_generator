{{file_header}}
%{
/*
 * This file is a template.
 */
#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <errno.h>

#include "string_buffer.h"
#include "alloc.h"
#include "errors.h"
#include "fileio.h"
#include "tokens.h"
#include "file_io.h"

int inline_depth = 0;
string_t* strbuf = NULL;
int yycolno = 1;
int prev_lineno;

#define MAX_INCL 16

#define YY_USER_ACTION update_numbers();

%}

%x INLINE_BLOCK DTEXT_BLOCK STEXT_BLOCK DQUOTE SQUOTE

%option yylineno
%option noinput
%option nounput
%option noyywrap

%%
   // int start_line, start_column;
   prev_lineno = yylineno;

{{gen_scanner_rules}}

    /* rules defined in the template. */

"inline"[ \n\r\t]*"{" {
    inline_depth = 0;
    if(strbuf == NULL)
        strbuf = create_string(NULL);
    BEGIN(INLINE_BLOCK);
}

<INLINE_BLOCK>"{" {
    inline_depth++;
    append_string_char(strbuf, '{');
}

<INLINE_BLOCK>[^{}\n] {
    append_string(strbuf, yytext);
}

<INLINE_BLOCK>\n {
    append_string_char(strbuf, '\n');
}

<INLINE_BLOCK>"}" {
    if(inline_depth > 0) {
        inline_depth--;
        append_string_char(strbuf, '}');
    }
    else {
        add_token_queue(create_token(copy_string(strbuf), TOK_INLINE));
        clear_string(strbuf);
        BEGIN(INITIAL);
        return TOK_INLINE;
    }
}

\" {
    if(strbuf == NULL)
        strbuf = create_string(NULL);
    BEGIN(DQUOTE);
}

<DQUOTE>\n {
    fprintf(stderr, "scanner error: %d: unexpected end of line in literal string\n", yylineno);
    BEGIN(INITIAL);
}

<DQUOTE>\\n { append_string_char(strbuf, '\n'); }
<DQUOTE>\\r { append_string_char(strbuf, '\r'); }
<DQUOTE>\\t { append_string_char(strbuf, '\t'); }
<DQUOTE>\\v { append_string_char(strbuf, '\v'); }
<DQUOTE>\\a { append_string_char(strbuf, '\a'); }
<DQUOTE>\\b { append_string_char(strbuf, '\b'); }
<DQUOTE>\\f { append_string_char(strbuf, '\f'); }
<DQUOTE>\\e { append_string_char(strbuf, '\x1b'); }
<DQUOTE>\\\" { append_string_char(strbuf, '\"'); }
<DQUOTE>\\. { append_string_char(strbuf, yytext[1]); }

<DQUOTE>\\[xX][0-9A-Fa-f]{1,4} {
    int tmp = strtol(&yytext[2], NULL, 16);
    append_string_char(strbuf, tmp);
}

<DQUOTE>\"[ \t]*\\[ \t]*\n[ \t]*\" { /* line ignore continuation */ }

<DQUOTE>[^\\\n\"]+ { append_string(strbuf, yytext); }

<DQUOTE>\" {
    add_token_queue(create_token(copy_string(strbuf), TOK_STRING_LITERAL));
    clear_string(strbuf);
    BEGIN(INITIAL);
    return TOK_STRING_LITERAL;
}

\' {
    if(strbuf == NULL)
        strbuf = create_string(NULL);
    BEGIN(SQUOTE);
}

<SQUOTE>\'[ \t]*\\[ \t]*\n[ \t]*\' { /* ignore continuation */ }

<SQUOTE>\n {
    fprintf(stderr, "scanner error: %d: unexpected end of line in literal string\n", yylineno);
    BEGIN(INITIAL);
}

<SQUOTE>\\ { append_string_char(strbuf, '\\'); }

<SQUOTE>[^\\\n\']+ { append_string(strbuf, yytext); }

<SQUOTE>\' {
    add_token_queue(create_token(copy_string(strbuf), TOK_STRING_LITERAL));
    clear_string(strbuf);
    BEGIN(INITIAL);
    return TOK_STRING_LITERAL;
}

\"{3,} {
    if(strbuf == NULL)
        strbuf = create_string(NULL);
    BEGIN(DTEXT_BLOCK);
}

<DTEXT_BLOCK>\"  { append_string_char(strbuf, '\"'); }
<DTEXT_BLOCK>(\\n|\n) { append_string_char(strbuf, '\n'); }
<DTEXT_BLOCK>\\r { append_string_char(strbuf, '\r'); }
<DTEXT_BLOCK>\\t { append_string_char(strbuf, '\t'); }
<DTEXT_BLOCK>\\v { append_string_char(strbuf, '\v'); }
<DTEXT_BLOCK>\\a { append_string_char(strbuf, '\a'); }
<DTEXT_BLOCK>\\b { append_string_char(strbuf, '\b'); }
<DTEXT_BLOCK>\\f { append_string_char(strbuf, '\f'); }
<DTEXT_BLOCK>\\e { append_string_char(strbuf, '\x1b'); }
<DTEXT_BLOCK>\\. { append_string_char(strbuf, yytext[1]); }

<DTEXT_BLOCK>\\[xX][0-9a-fA-F]{1,4} {
    int tmp = strtol(&yytext[2], NULL, 16);
    append_string_char(strbuf, tmp);
}

<DTEXT_BLOCK>[^\"\n\\]+ { append_string(strbuf, yytext); }

<DTEXT_BLOCK>\"{3,} {
    add_token_queue(create_token(copy_string(strbuf), TOK_STRING_LITERAL));
    clear_string(strbuf);
    BEGIN(INITIAL);
    return TOK_STRING_LITERAL;
}

\'{3,} {
    if(strbuf == NULL)
        strbuf = create_string(NULL);
    BEGIN(STEXT_BLOCK);
}

<STEXT_BLOCK>\' { append_string_char(strbuf, '\''); }
<STEXT_BLOCK>\n { append_string_char(strbuf, ' '); }
<STEXT_BLOCK>\\. { append_string_char(strbuf, yytext[1]); }
<STEXT_BLOCK>[^\'\n]+ { append_string(strbuf, yytext); }

<STEXT_BLOCK>\'{3,} {
    add_token_queue(create_token(copy_string(strbuf), TOK_STRING_LITERAL));
    clear_string(strbuf);
    BEGIN(INITIAL);
    return TOK_STRING_LITERAL;
}

[a-zA-Z_][0-9a-zA-Z_]*  {
    add_token_queue(create_token(create_string(yytext), TOK_IDENTIFIER));
    return TOK_IDENTIFIER;
}

(([1-9][0-9]*\.[0-9]+)|(0\.[0-9]+))([eE][-+]?[0-9]+)? {
    add_token_queue(create_token(create_string(yytext), TOK_FLOAT_LITERAL));
    return TOK_FLOAT_LITERAL;
}

([1-9][0-9]*)|0 {
    add_token_queue(create_token(create_string(yytext), TOK_INT_LITERAL));
    return TOK_INT_LITERAL;
}

(";"|"#")[^\n]*\n  { /* ignore comment */ }

[ \t\r\n\v\f]+ { /* ignore spaces */ }

. { fprintf(stderr, "scanner error: %d: unexpected character: %c (0x%02X)\n", yylineno, yytext[0], yytext[0]); }

<<EOF>> {
    add_token_queue(create_token(create_string(NULL), TOK_END_OF_FILE));
    yyterminate(); // return NULL
}

%%

